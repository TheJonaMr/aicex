* Comparator testbench
**********************************************************************
**        Copyright (c) 2022 Carsten Wulff Software, Norway
** *******************************************************************
** Created       : wulff at 2022-5-16
** *******************************************************************
**  The MIT License (MIT)
**
**  Permission is hereby granted, free of charge, to any person obtaining a copy
**  of this software and associated documentation files (the "Software"), to deal
**  in the Software without restriction, including without limitation the rights
**  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
**  copies of the Software, and to permit persons to whom the Software is
**  furnished to do so, subject to the following conditions:
**
**  The above copyright notice and this permission notice shall be included in all
**  copies or substantial portions of the Software.
**
**  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
**  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
**  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
**  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
**  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
**  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
**  SOFTWARE.
**
**********************************************************************


*----------------------------------------------------------------
* Include
*----------------------------------------------------------------
.include ../build/SUN_SAR9B_SK130NM.spi

*----------------------------------------------------------------
* Options
*----------------------------------------------------------------
*.option TNOM=27 GMIN=1e-15 reltol=1e-6 abstol=1e-6

.option reltol=1e-5

*----------------------------------------------------------------
* PARAMETERS
*----------------------------------------------------------------
.param TRF = 10p

*- Analog parameters
.param AVDD = 1.5

*- 10 MHz clock frequency
.param PERIOD_CLK = 100n

*- 100 MHz clock frequency
.param PERIOD_CMP_CLK = 10n

*- 25% duty-cycle clock
.param PW_CLK = PERIOD_CLK/4

*- 50% duty-cycle comparator clock
.param PW_CMP_CLK = PERIOD_CMP_CLK/2

*- Frequency bin of the input signal
.param fbin = 5

*- number of cycles in FFT
.param nbpt = 64

*- Sampling frequency
.param fs = 1/PERIOD_CLK

*- Input frequency for coherent sampling
.param fin = fbin/nbpt*fs

.param vamp = 0.25

*----------------------------------------------------------------
* Sources
*----------------------------------------------------------------
VSS  AVSS 0 dc 0
VDD  AVDD AVSS dc AVDD
VREF VREF AVSS dc AVDD


*- Control signals
VCLK CLK 0 dc 0 pulse (0 AVDD 0 TRF TRF PW_CLK PERIOD_CLK)
VCLK_CMP CK_CMP 0 dc 0 pulse (0 AVDD 0 TRF TRF PW_CMP_CLK PERIOD_CMP_CLK)



X3 CLK CK_SAMPLE_BSSW AVDD AVSS AVDD AVSS BFX1_CV
VCM VCM 0 dc {AVDD/2}
VSARP SAR_IP VCM sin (0 vamp fin)
VSARN SAR_IN VCM sin (0 {-vamp} fin)



*----------------------------------------------------------------
* DUT
*----------------------------------------------------------------

XDUT1 SAR_IP CK_SAMPLE_BSSW NCCA CEIN SARP SARN AVDD AVSS SARBSSW_CV
XDUT2 SAR_IN CK_SAMPLE_BSSW NCCB CEIN SARN SARP AVDD AVSS SARBSSW_CV

XXA2 DONE AVDD AVSS AVDD AVSS TIEL_CV

XXA20 SARP SARN CMP_OP CMP_ON CK_CMP CK_SAMPLE_BSSW DONE AVDD AVSS SARCMPX1_CV


C1 SARP 0 0.5p
C2 SARN 0 0.5p


BDO VOD 0 V=V(SARP)-V(SARN)
BD1 VOCMP 0 V=V(CMP_ON)-V(CMP_OP)


*----------------------------------------------------------------
* NGSPICE control
*----------------------------------------------------------------
.probe v(SARP) v(SARN) v(vod) v(vocmp)

.control
set color0=white
set color1=black
unset askquit
tran 1n 1u
*plot v(SARP) v(SARN) v(SAR_IP) v(SAR_IN)
*plot v(VOD)
write

hardcopy {cicfname}.ps v(vod) v(vocmp)

quit



.endc
.end
